      SUBROUTINE MRSL01(A,B,N,NL,IER,INDIC)
      IMPLICIT REAL*8 (A-H,O-Z)
C
C     SOUS PROGRAMME DE RESOLUTION DE SYSTEMES LINEAIRES
C
C     A   MATRICE PREMIER MEMBRE
C     B   EN ENTREE VECTEUR SECOND MEMBRE
C         EN SORTIE VECTEUR SOLUTION
C     N   NOMBRE D'EQUATIONS
C     NL  NOMBRE DE LIGNES REEL DE LA MATRICE A (DIMENSION)
C     IER INDICATEUR D'ERREUR
C         0 AUCUNE ERREUR DETECTEE
C         1 PIVOT TROP PETIT EN COURS D'ELIMINATION
C     INDIC  NON UTILISE DANS CETTE VERSION
C
C     ATTENTION  LA MATRICE A EST DETRUITE PAR LE SOUS PROGRAMME
C
C
      REAL*8 PIVOT,PIVOTX
      DIMENSION A(NL,1) ,B(NL,1)
      M=1
      IER=0
      EPS=1.E-20
      DO 60 K=1,N
      K1=K+1
      IF(K.EQ.N) GO TO 30
      L=K
      DO 10 J=K1,N
C
C     RECHERCHE DU PLUS GRAND ELEMENT DANS LA COLONNE K
C
      IF(ABS(A(J,K)).GT.ABS(A(L,K))) L=J
  10  CONTINUE
      IF(L.EQ.K) GO TO 30
C
C     ECHANGE LIGNES K ET L
C
      DO 20 J=K,N
      T=A(K,J)
      A(K,J)=A(L,J)
  20  A(L,J)=T
      DO 22 J=1,M
      T=B(K,J)
      B(K,J)=B(L,J)
  22  B(L,J)=T
C
C     SI PIVOT INFERIEUR A EPS IER=1
C
  30  PIVOT=A(K,K)
      IF(ABS(PIVOT).LT.EPS) IER=1
      IF(K.EQ.N) GO TO 41
      DO 40 J=K1,N
C
C     DIVISION DE LA LIGNE PIVOT PAR LE PIVOT
C
  40  A(K,J)=A(K,J)/PIVOT
  41  CONTINUE
      DO 42 J=1,M
  42  B(K,J)=B(K,J)/PIVOT
      IF(K.EQ.N) GO TO 60
C
C     ELIMINATION
C
      DO 50 I=K1,N
      PIVOTX=A(I,K)
      DO 45 J=K1,N
  45  A(I,J)=A(I,J)-PIVOTX*A(K,J)
      DO 47 J=1,M
  47  B(I,J)=B(I,J)-A(I,K)*B(K,J)
  50  CONTINUE
  60  CONTINUE
C
C     SUBSTITUTION
C
      DO 70 M1=1,M
      DO 70 J=2,N
      K=N+2-J
      K1=K-1
      PIVOTX=B(K,M1)
      DO 70 I=1,K1
  70  B(I,M1)=B(I,M1)-A(I,K)*PIVOTX
      RETURN
      END
