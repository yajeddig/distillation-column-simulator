      SUBROUTINE MRSL21(A,B,C,F,N,M,N1A,NLA,NC,IER,IRAZ,IMET)
      IMPLICIT REAL*8 (A-H,O-Z)
C*
C*    RESOLUTION D'UN SYSTEME LINEAIRE DONT LA MATRICE
C*    PREMIER MEMBRE EST TRIDIAGONALE PAR BLOCS
C*    CREE LE 19-03-85
C*
C     LA MATRICE PREMIER MEMBRE SE PRESENTE SOUS LA FORME:
C      B1 C1
C      A2 B2 C2
C         A3 B3 C3
C
C              ..........
C
C                 AM BM
C
C     CETTE VERSION PERMET DANS CERTAINS CAS REELS D'ECONOMISER
C     PLUS D'UN TIERS DE LA PLACE MEMOIRE UTILISE AVEC L'ALGORITHME
C     TRADITIONNEL EN UTILISANT COMME DIMENSIONS :
C       A(NLA,N1A:N,M) PARTIE NON NULLE DE A COLLEE A B
C       B(N,N,M)
C       C(N,NC,M) PARTIE NON NULLE DE C COLLEE A B
C
C     ON PEUT AVOIR N1A=1 , NLA=N , NC=N CE QUI REVIENT AU
C     PROGRAMME TRADITIONNEL TEL QUE M7EXTR
C
C     PARAMETRES DU S.P.
C     -----------------
C     A,B,C DESCRIPTION MATRICE PREMIER MEMBRE VOIR CI DESSUS
C     F VECTEUR 2EME MEMBRE EN ENTREE
C       VECTEUR SOLUTION EN SORTIE
C             ATTENTION: A,B,C ET F DETRUITS PAR LE S.P.
C
C     N NOMBRE D'EQUATIONS PAR BLOC
C       SERT AU DIMENSIONNEMENT POUR A B C ET F
C     N1A 1ER INDICE COLONNE DE A UTILISE (1 A N) ATTENTION
C     NLA NOMBRE DE LIGNES UTILES DE A (1 A N) ATTENTION
C     NC  NOMBRES DE COLONNES UTILES DE C (1 A N) ATTENTION
C
C     IER INDICATEUR D'ERREUR  DOIT ETRE TESTE EN SORTIE
C         0  VALEUR NORMALE
C         >0 NOMBRE DE PIVOTS < EPS
C         <0 PIVOT NUL RENCONTRE  CALCUL ARRETE NE PAS UTILISER
C               LE RESULTAT
C
C     IRAZ PARAMETRE D'ENTREE
C          0 LES TABLEAUX A B C DOIVENT ETRE ENTIEREMENT REINITIALISES
C            PAR L'UTILISATEUR (MEME LES ELEMENTS NULS)
C          1 EN SORTIE DU S.P. LES TABLEAUX A B C SONT REMIS A ZERO
C
C     IMET PARAMETRE D'ENTREE
C          O PAS DE RECHERCHE DE PIVOT MAXIMUM
C          1 RECHERCHE DE PIVOT MAXIMUM POUR CHAQUE BLOC B
C
C     CE S.P. NE FONCTIONNE QU'EN FORTRAN 77
C     POUR PASSER EN FORTRAN IV ON NE PEUT PLUS UTILISER
C     LES AVANTAGES DES 0 CONNUS SUR A , MODIFIER L'INSTRUCTION
C     DIMENSION (EN FAIT N1A NLA RESTENT UTILISABLES MAIS LA
C     TAILLE MEMOIRE DE A EST PLUS GRANDE)
C
C
      DIMENSION A(NLA,N1A:N,M),B(N,N,M),C(N,NC,M),F(N,M)
C
C     EPS VALEUR EN DESSOUS DE LAQUELLE UN PIVOT EST CONSIDERE
C     COMME TROP PETIT  (IER INCREMENTE  CALCUL POURSUIVI)
C     SI UN PIVOT NUL EST RENCONTRE IER RECOIT UNE VALEUR NEGATIVE
C     ET LE CALCUL EST ARRETE (VERIFIER IER EN SORTIE)
C
      EPS=1.E-12
      IER=0
C
C     ELIMINATION
C
C       POUR LES M-1 PREMIERS BLOCS (LE DERNIER EST TRAITE A PART)
C
      DO 100 L=1,M-1
      L1=L+1
C     K INDICE DE LIGNE PIVOT
C
C
      DO 90 K=1,N
      K1=K+1
C
C     PIVOT
C
C
C     RECHERCHE DE PIVOT MAXIMUM SI IMET > 0
C
      IF(IMET.EQ.0) GO TO 9
      PIVOT=ABS(B(K,K,L))
      IK=K
      DO 5 I=K1,N
      IF(ABS(B(I,K,L)).LE.PIVOT) GO TO 5
C     ON A TROUVE ICI LIGNE IK UN PIVOT PLUS GRAND
      PIVOT=ABS(B(I,K,L))
      IK=I
  5   CONTINUE
      IF(IK.EQ.K) GO TO 9
C
C     PIVOT MAXIMUM TROUVE LIGNE IK , INVERSION DES
C     LIGNES IK ET K
C
      DO 7 J=K,N
      T=B(K,J,L)
      B(K,J,L)=B(IK,J,L)
  7   B(IK,J,L)=T
      DO 8 J=1,NC
      T=C(K,J,L)
      C(K,J,L)=C(IK,J,L)
  8   C(IK,J,L)=T
      T=F(K,L)
      F(K,L)=F(IK,L)
      F(IK,L)=T
  9   CONTINUE
C
C
      PIVOT=B(K,K,L)
      IF(ABS(PIVOT).LT.EPS) IER=IER+1
      IF(ABS(PIVOT).LT.1.E-30) GO TO 300
C     ELEMENTS DE B A DROITE DU PIVOT
      DO 10 J=K1,N
 10   B(K,J,L)=B(K,J,L)/PIVOT
C     ELEMENTS DE C
      DO 20 J=1,NC
 20   C(K,J,L)=C(K,J,L)/PIVOT
C     2EME MEMBRE
      F(K,L)=F(K,L)/PIVOT
C
C     ELIMINATION DES TERMES DE B COLONNE K
C
      DO 50 I=K1,N
      IF(B(I,K,L).EQ.0) GO TO 50
      PIVOTX=B(I,K,L)
C     ELEMENTS DE B
      DO 30 J=K1,N
 30   B(I,J,L)=B(I,J,L)-PIVOTX*B(K,J,L)
C     ELEMENTS DE C
      DO 40 J=1,NC
 40   C(I,J,L)=C(I,J,L)-PIVOTX*C(K,J,L)
C     2EME MEMBRE
      F(I,L)=F(I,L)-PIVOTX*F(K,L)
 50   CONTINUE
C
C     ELIMINATION DES TERMES DE A INDICE L+1  COLONNE K
C
      IF(K.LT.N1A) GO TO 90
C
      DO 80 I=1,NLA
      PIVOTX=A(I,K,L1)
      IF(PIVOTX.EQ.0) GO TO 80
C     ELEMENTS DE A A DROITE DE LA COLONNE K
      DO 60 J=K1,N
 60   A(I,J,L1)=A(I,J,L1)-PIVOTX*B(K,J,L)
C     ELEMENTS DE B
C     SEULS LES ELEMENTS 1 A NC SONT AFFECTES
      DO 70 J=1,NC
 70   B(I,J,L1)=B(I,J,L1)-PIVOTX*C(K,J,L)
C     LES ELEMENTS DE C INDICE L1 NE SONT PAS AFFECTES
C     ELEMENTS DE F
      F(I,L1)=F(I,L1)-PIVOTX*F(K,L)
 80   CONTINUE
C
 90   CONTINUE
C
100   CONTINUE
C
C     CAS PARTICULIER DU DERNIER BLOC
C     TRAITEMENT DE B UNIQUEMENT (A DEJA NULLE)
C
      L=M
      DO 130 K=1,N
      K1=K+1
C
C     LIGNE PIVOT K
C
C
C     RECHERCHE DE PIVOT MAXIMUM SI IMET > 0
C
      IF(IMET.EQ.0) GO TO 109
      PIVOT=ABS(B(K,K,L))
      IK=K
      DO 105 I=K1,N
      IF(ABS(B(I,K,L)).LE.PIVOT) GO TO 105
C     ON A TROUVE ICI LIGNE IK UN PIVOT PLUS GRAND
      PIVOT=ABS(B(I,K,L))
      IK=I
105   CONTINUE
      IF(IK.EQ.K) GO TO 109
C
C     PIVOT MAXIMUM TROUVE LIGNE IK , INVERSION DES
C     LIGNES IK ET K
C
      DO 107 J=K,N
      T=B(K,J,L)
      B(K,J,L)=B(IK,J,L)
107   B(IK,J,L)=T
      DO 108 J=1,NC
      T=C(K,J,L)
      C(K,J,L)=C(IK,J,L)
108   C(IK,J,L)=T
      T=F(K,L)
      F(K,L)=F(IK,L)
      F(IK,L)=T
109   CONTINUE
C
C
      PIVOT=B(K,K,L)
      IF(ABS(PIVOT).LT.EPS) IER=IER+1
      IF(ABS(PIVOT).LT.1.E-30) GO TO 300
C     ELEMENTS DE B A DROITE DU PIVOT
      DO 110 J=K1,N
110   B(K,J,L)=B(K,J,L)/PIVOT
C     2EME MEMBRE
      F(K,L)=F(K,L)/PIVOT
C
C     ELIMINATION DES TERMES DE B COLONNE K
C
      DO 120 I=K1,N
      PIVOTX=B(I,K,L)
      IF(PIVOTX.EQ.0) GO TO 120
C     ELEMENTS DE B
      DO 115 J=K1,N
115   B(I,J,L)=B(I,J,L)-PIVOTX*B(K,J,L)
C     2EME MEMBRE
      F(I,L)=F(I,L)-PIVOTX*F(K,L)
120   CONTINUE
C
130   CONTINUE
C
C
C     SUBSTITUTION INVERSE
C
C       BLOCS M A 2
C
      DO 180 L=M,2,-1
      L1=L-1
C
C     APPARITION D'UN ELEMENT XL DE LA SOLUTION DANS F(K,L)
C
      DO 170 K=N,1,-1
      XK=F(K,L)
C     ELEMENTS DE B
      DO 150 I=1,K-1
150   F(I,L)=F(I,L)-XK*B(I,K,L)
C     ELEMENTS DE C
      IF(K.GT.NC) GO TO 170
      DO 160 I=1,N
160   F(I,L1)=F(I,L1)-XK*C(I,K,L1)
170   CONTINUE
C
180   CONTINUE
C
C     TERMINER LA SUBSTITUTION POUR LE PREMIER BLOC
C
      L=1
      DO 200 K=N,2,-1
      XK=F(K,L)
      DO 190 I=1,K-1
190   F(I,L)=F(I,L)-XK*B(I,K,L)
200   CONTINUE
C
C     TERMINE
C
      IF(IRAZ.EQ.0) RETURN
C
C     SI IRAZ DIFFERENT DE ZERO REMETTRE A ZERO A B ET C
C
      DO 210 L=2,M
      DO 210 I=1,NLA
      DO 210 J=N1A,N
210   A(I,J,L)=0
      DO 220 L=1,M
      DO 220 I=1,N
      DO 220 J=1,N
220   B(I,J,L)=0
      DO 230 L=1,M-1
      DO 230 I=1,N
      DO 230 J=1,NC
230   C(I,J,L)=0
C
      RETURN
C
C     PIVOT NUL ERREUR  ET ARRET DU TRAITEMENT
C
300   CONTINUE
      IER=-IER
      RETURN
      END
